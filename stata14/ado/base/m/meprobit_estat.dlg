/*
	meprobit_estat

*!  VERSION 1.1.1  02feb2015

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 400
INCLUDE header

HELP hlp1, view("help meprobit postestimation")
RESET res1

SCRIPT PREINIT
BEGIN
	create BOOLEAN has_FactorVars
	has_FactorVars.settrue
	program sum_bu_facvars_ckResults
	program vce_bu_facvars_ckResults
	setscale_hadj resource MEPROBIT_ESTAT_DLG_HSCALE
END


SCRIPT POSTINIT
BEGIN
	program parseMessage
END

PROGRAM parseMessage
BEGIN
	if __MESSAGE.contains("-group-") {
		call main.lb_subcmd.setvalue "group"
	}
	if __MESSAGE.contains("-icc-") {
		call main.lb_subcmd.setvalue "icc"
	}
	if __MESSAGE.contains("-ic-") {
		call main.lb_subcmd.setvalue "ic"
	}
	if __MESSAGE.contains("-summarize-") {
		call main.lb_subcmd.setvalue "summarize"
	}
	if __MESSAGE.contains("-vce-") {
		call main.lb_subcmd.setvalue "vce"
	}
END

DIALOG main, label(resource MEPROBIT_ESTAT_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT     tx_subcmd		_lft	_top	_cwd1	.,		///
	label(resource LBL_REPORTS_STATISTICS_SUBCOMMAND)
  LISTBOX  lb_subcmd		@	_ss	_iwd	70,		///
	contents(main_subcommand_contents)				///
	values(main_subcommand_values)					///
	onselchangelist(main_subcommand_scripts)

  DEFINE holdy 110
  // group
  TEXT     tx_gpnote		_lft	holdy	_iwd	.,		///
	label(resource LBL_CLICK_OK_SUBMIT_SUM_COMPOSITION_NESTED_GROUPS)

  // Information criteria
  CHECKBOX ck_n			_lft	holdy	_iwd	.,		///
	onclickon(main.sp_n.enable)					///
	onclickoff(main.sp_n.disable)					///
	label(resource LBL_NUMBER_OBSERVATIONS_CALCULATING_BIC)
  SPINNER sp_n			_indent2 _ss	_spwd	.,		///
	default(e(N)) min(1) max(e(N))					///
	option(n)

  // Intraclass correlation
  DEFINE   _x  _lft
  DEFINE   _cx _spr
  DEFINE   _y  holdy
  INCLUDE  _sp_level

  INCLUDE estat_sum
  INCLUDE estat_vce
END

INCLUDE estat_sum_pr
INCLUDE estat_vce_pr

LIST main_subcommand_contents
BEGIN
	resource LBL_SUM_COMPOSITION_NESTED_GROUPS_GROUP
	resource LBL_INTRACLASS_CORRELATION_ICC
	resource LBL_INFORMATION_CRITERIA
	resource LBL_SUMMARIZE_ESTIMATION_SAMPLE
	resource LBL_COVARIANCE_MATRIX_ESTIMATES
END

LIST main_subcommand_values
BEGIN
	group
	icc
	ic
	summarize
	vce
END

LIST main_subcommand_scripts
BEGIN
	script sel_group
	script sel_icc
	script sel_ic
	script sel_summarize
	script sel_vce
END

SCRIPT sel_group
BEGIN
	main.ck_n.hide
	main.sp_n.hide
	main.sp_level.hide
	main.tx_level.hide
	script main_summ_off
	program vce_off
	script non_basic_opt_off
	main.tx_gpnote.show
END

SCRIPT sel_ic
BEGIN
	main.ck_n.show
	main.sp_n.show
	main.sp_level.hide
	main.tx_level.hide
	script main_summ_off
	program vce_off
	script non_basic_opt_off
END

SCRIPT sel_icc
BEGIN
	main.ck_n.hide
	main.sp_n.hide
	main.sp_level.show
	main.tx_level.show
	script main_summ_off
	program vce_off
	script non_basic_opt_off
END

SCRIPT sel_summarize
BEGIN
	main.ck_n.hide
	main.sp_n.hide
	main.sp_level.hide
	main.tx_level.hide
	program main_summ_on
	program vce_off
	script non_basic_opt_off
END

SCRIPT sel_vce
BEGIN
	main.ck_n.hide
	main.sp_n.hide
	main.sp_level.hide
	main.tx_level.hide
	program vce_on
	script main_summ_off
	script non_basic_opt_off
END

SCRIPT non_basic_opt_off
BEGIN
	main.tx_gpnote.hide
END

PROGRAM vl_output
BEGIN
	put main.vl_spec
END

PROGRAM vl_eq_output
BEGIN
	put main.vl_eq
END

PROGRAM command
BEGIN
	put "estat "
	put main.lb_subcmd
	put " " /program summarize_output
	beginoptions
		// ic options
		optionarg main.sp_n

		// icc options
		optionarg /hidedefault main.sp_level

		// Summarize options
		put " " /program summarize_opts_output

		put /program vce_output
	endoptions
END
