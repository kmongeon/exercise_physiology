/*
	predict (after mlogit)

*!  VERSION 1.2.0  29oct2014

*/

VERSION 14.0

INCLUDE _std_large
INCLUDE _ht290
INCLUDE header

HELP hlp1, view("help mlogit postestimation")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource MLOGIT_P_DLG_HSCALE
END


DIALOG main, label(resource PREDICT_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  INCLUDE  _p_gentype

  GROUPBOX gb_opts           _lft      _ls       _iwd      _ht19,	/*
		*/ label(resource LBL_PRODUCE)
  RADIO    rb_p_all          _ilft     _ss       _ibwd     ., first	/*
		*/ option(p)						/*
		*/ clickon("script offboth")				/*
		*/ label(resource LBL_PROBABILITIES_SPECIFY_AS_MANY_VAR_AS_NUM_OUT)
  RADIO    rb_score          _ilft     _ss       _comb3_2  .,		/*
		*/ option(scores)					/*
		*/ clickon("script offboth")				/*
		*/ label(resource LBL_EQUATION_LEVEL_SCORES)
  DEFINE   holdy@y
  RADIO    rb_p              _ilft     _ss       _comb3_2  .,		/*
		*/ option(p)						/*
		*/ clickon("script off2")				/*
		*/ label(resource LBL_PROBABILITIES_SPECIFY_ONE_VAR)
  RADIO    rb_xb             @         _ss       @         .,		/*
		*/ option(xb)						/*
		*/ clickon("script off2")				/*
		*/ label(resource LBL_LINEAR_PREDICTION_XB)
  RADIO    rb_stdp           @         _ss       @         .,		/*
		*/ option(stdp)						/*
		*/ clickon("script off2")				/*
		*/ label(resource LBL_STANDARD_ERROR_LINEAR_PREDICTION)
  RADIO    rb_stddp          @         _ss       @         ., last	/*
		*/ option(stddp)					/*
		*/ clickon("script on2")				/*
		*/ label(resource LBL_STANDARD_ERROR_OF_THE_DIFFERENCE)

  TEXT     tx_outcome1       _ilft3_3  holdy     _cwd3_2   .,		/*
		*/ label(resource LBL_OUTCOME)
  LISTBOX  lb_outcome1       @         _ss       @         _ht3,	/*
		*/ contents(ereturn eqnames)				/*
		*/ label(resource LBL_OUTCOME)
  TEXT     tx_outcome2       @         _vs3      @         .,		/*
		*/ label(resource MLOGIT_P_MAIN_TX_OUTCOME2)
  LISTBOX  lb_outcome2       @         _ss       @         _ht3,	/*
		*/ contents(ereturn eqnames)				/*
		*/ label(resource MLOGIT_P_MAIN_TX_OUTCOME2)
END

INCLUDE _p_gentype_sc
INCLUDE ifin
INCLUDE _type_list_fd

SCRIPT offboth
BEGIN
	script _p_gentype_multiple
	script eq1off
	script eq2off
END

SCRIPT off2
BEGIN
	script _p_gentype_single
	script eq1on
	script eq2off
END

SCRIPT on2
BEGIN
	script _p_gentype_single
	script eq1on
	script eq2on
END

SCRIPT eq2on
BEGIN
	main.tx_outcome2.enable
	main.lb_outcome2.enable
END

SCRIPT eq2off
BEGIN
	main.tx_outcome2.disable
	main.lb_outcome2.disable
END

SCRIPT eq1on
BEGIN
	main.tx_outcome1.enable
	main.lb_outcome1.enable
END

SCRIPT eq1off
BEGIN
	main.tx_outcome1.disable
	main.lb_outcome1.disable
END

PROGRAM command
BEGIN
	put "predict "
	put " " /program _p_gentype_output
	INCLUDE _ifin_pr
	beginoptions
		if !main.rb_p_all & !main.rb_score {
			put "outcome("
			require main.lb_outcome1
			put main.lb_outcome1
			if main.rb_stddp {
				require main.lb_outcome2
				put ", " main.lb_outcome2
			}
			put ") "
		}
		option radio(main rb_p_all rb_p rb_xb rb_stdp /*
			*/ rb_stddp rb_score)
	endoptions
END
