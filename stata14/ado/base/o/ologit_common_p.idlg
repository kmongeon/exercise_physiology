/*
	ologit_common_p.idlg

	This is an INCLUDE file for the -predict- dialogs that go with
	-ologit- and -svy: ologit-.

*!  VERSION 1.1.0  06nov2014

*/

DIALOG main, label(resource PREDICT_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT     tx_vars           _lft      _top      _vlwd     .,		/*
		*/ label(resource LBL_NEW_VARIABLE_NAMES_STUB)
  EDIT     ed_vars           @         _ss       @         .,		/*
		*/ label(resource LBL_NEW_VARIABLE_NAMES_STUB)
  TEXT     tx_var            _lft      _top      @         .,		/*
		*/ label(resource LBL_NEW_VARIABLE_NAME)
  EDIT     ed_var            @         _ss       _vnwd     .,		/*
		*/ label(resource LBL_NEW_VARIABLE_NAME) max(32)

  TEXT     tx_type           _vlsep    _top      _vlr      .,		/*
		*/ label(resource LBL_NEW_VARIABLE_TYPE)
  COMBOBOX cb_type           @         _ss       @         .,		/*
		*/ dropdownlist						/*
		*/ contents(type) default(c(type))			/*
		*/ label(resource LBL_TYPE)

  GROUPBOX gb_opts           _lft      _ls       _iwd      _ht14h,	/*
		*/ label(resource LBL_PRODUCE)
  RADIO    rb_p_all          _ilft     _ss       _ibwd     ., first	/*
		*/ option(p)						/*
		*/ clickon("script manyon")				/*
    		*/ label(resource LBL_PROBABILITIES_SPECIFY_AS_MANY_VAR_AS_NUM_OUT)
  RADIO    rb_score          @         _ss       @         .,		/*
		*/ option(scores)					/*
		*/ clickon("script score_on")				/*
		*/ label(resource LBL_EQUATION_LEVEL_SCORES)
  RADIO    rb_p              @         _ss       @         .,		/*
		*/ option(p)						/*
		*/ clickon("script oneon_p")				/*
		*/ label(resource LBL_PROBABILITIES_SPECIFY_ONE_VAR)
  TEXT     tx_outcome        _indent2  _ss       _inwd     .,		/*
		*/ label(resource LBL_FOR_OUTCOME_VALUE_DEPVAR)
  EDIT     ed_outcome        @         _ss       _vnwd     .,		/*
		*/ option(outcome)					/*
		*/ label(resource LBL_FOR_OUTCOME_VALUE_DEPVAR)

  RADIO    rb_xb             _ilft     _ms       _ibwd     .,		/*
		*/ option(xb)						/*
		*/ clickon("script oneon")				/*
		*/ label(resource LBL_LINEAR_PREDICTION_XB)
  RADIO    rb_stdp           @         _ss       @         ., last	/*
		*/ option(stdp)						/*
		*/ clickon("script oneon")				/*
		*/ label(resource LBL_STANDARD_ERROR_OF_LINEAR_PREDICTION)

  INCLUDE  _p_nooffset
END


INCLUDE _p_gentype_sc
INCLUDE ifin
INCLUDE _type_list_fd


SCRIPT score_on
BEGIN
	script manyon
	main.ck_nooffset.disable
END

SCRIPT manyon
BEGIN
	script manyvars
	script eqoff
	main.ck_nooffset.enable
END

SCRIPT oneon
BEGIN
	script onevar
	script eqoff
	main.ck_nooffset.enable
END

SCRIPT oneon_p
BEGIN
	script onevar
	script eqon
	main.ck_nooffset.enable
END

SCRIPT manyvars
BEGIN
	main.tx_vars.show
	main.ed_vars.show
	main.tx_var.hide
	main.ed_var.hide
END

SCRIPT onevar
BEGIN
	main.tx_vars.hide
	main.ed_vars.hide
	main.tx_var.show
	main.ed_var.show
END

SCRIPT eqon
BEGIN
	main.tx_outcome.enable
	main.ed_outcome.enable
END

SCRIPT eqoff
BEGIN
	main.tx_outcome.disable
	main.ed_outcome.disable
END


PROGRAM command
BEGIN
	put "predict "
	if main.cb_type.isdefault() {
		if main.rb_p_all | main.rb_score {
			require main.ed_vars
			put main.ed_vars " "
		}
		if !main.rb_p_all & !main.rb_score {
			require main.ed_var
			put main.ed_var " "
		}
	}
	if ! main.cb_type.isdefault() {
		put main.cb_type " " "("
		if main.rb_p_all | main.rb_score {
			require main.ed_vars
			put main.ed_vars
		}
		if !main.rb_p_all & !main.rb_score {
			require main.ed_var
			put main.ed_var
		}
		put ") "
	}

	INCLUDE _ifin_pr
	beginoptions
		if main.rb_p {
			require main.ed_outcome
			optionarg main.ed_outcome
		}
		option radio(main rb_p_all rb_p rb_xb rb_stdp rb_score)
	endoptions
	INCLUDE _p_nooffset_pr
END
