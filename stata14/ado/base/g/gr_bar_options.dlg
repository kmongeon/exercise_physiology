/*
    gr_bar_options.dlg
    
*!  VERSION 1.1.0  07oct2014

	// SCRIPTS and PROGRAMS for external use
		* SCRIPT  gr_bar_setHistogramOn

*/

VERSION 14

INCLUDE _std_vsmall
DEFINE _dlght 255
INCLUDE header_gr_child

DEFINE _clwd	15
DEFINE _wd	140
DEFINE _sep	+145
DEFINE _cbwd	130
DEFINE _cbsep	+135

HELP hlp1, view("help twoway_bar")
RESET res1

SCRIPT  gr_bar_setHistogramOn
BEGIN
	main.tx_base.hide
	main.ed_base.hide
	main.tx_gap.show
	main.sp_gap.show
	histogramOn.settrue
END

SCRIPT PREINIT
BEGIN
        setscale_hadj resource GR_BAR_OPTIONS_DLG_HSCALE
END

PROGRAM PREINIT_PROGRAM
BEGIN
	call main.tx_gap.hide
	call main.sp_gap.hide
	call create PBOOLEAN histogramOn
	if histogramOn {
		call script gr_bar_setHistogramOn
	}
END

DIALOG main, label(resource LBL_LINE_OPTIONS)
BEGIN
  TEXT     tx_fclr	_lft	20	_wd	.,		///
	label(resource LBL_FILL_COLOR) right
  COMBOBOX cb_fclr	_sep	@	_cbwd	.,		///
	dropdownlist contents(colors) option(fcolor)		///
	onselchangelist(main_cl_fclr_sel)
  COLOR    cl_fclr	_cbsep	@	_clwd	., option(fcolor)

  TEXT     tx_intensity	_lft	_mls	_wd	.,		///
	label(resource LBL_FILL_INTENSITY) right
  EDIT     ed_intensity	_sep	@	_cbwd	.,		///
	option(fintensity)
	
  TEXT     tx_clr	_lft	_mls	_wd	.,		///
	label(resource LBL_OUTLINE_COLOR) right
  COMBOBOX cb_clr	_sep	@	_cbwd	.,		///
	dropdownlist contents(colors) option(lcolor)		///
	onselchangelist(main_cl_clr_sel)
  COLOR    cl_clr	_vnsep	@	_clwd	., option(lcolor)

  TEXT     tx_width	_lft	 _mls	_wd	.,		///
	label(resource LBL_OUTLINE_WIDTH) right
  COMBOBOX cb_width	_sep	@	_cbwd	.,		///
	dropdown option(lwidth)					///
	contents(linewidths)

  TEXT     tx_lpattern	_lft	_mls	_wd	.,		///
	label(resource LBL_OUTLINE_PATTERN) right
  COMBOBOX cb_lpattern	_sep	@	_cbwd	.,		///
	dropdownlist option(lpattern)				///
	contents(linepatterns)
	
  TEXT     tx_orient	_lft	_mls	_wd	.,		///
	label(resource LBL_ORIENTATION) right
  COMBOBOX cb_orient	_sep	@	_cbwd	.,		///
	dropdownlist 						///
	contents(orientation_contents)  			///
	values(orientation_values)
	
  TEXT     tx_barwidth	_lft	_mls	_wd	.,		///
	label(resource LBL_BAR_WIDTH) right
  EDIT     ed_barwidth	_sep	@	_cbwd	.,		///
	option(barwidth)
	
  TEXT     tx_base	_lft	_mls	_wd	.,		///
	label(resource LBL_BASE_LINE) right
  EDIT     ed_base	_sep	@	_cbwd	.,		///
	option(base)

  /* for histogram */
  TEXT     tx_gap	_lft	@	_wd	.,		///
	label(resource LBL_BAR_GAP) right
  SPINNER  sp_gap	_sep	@	_cbwd	.,		///
	option(gap) min(0) max(100) default(0)
END

LIST main_cl_clr_sel
BEGIN
	main.cl_clr.hide
	main.cl_clr.show
	main.cl_clr.hide
END

LIST main_cl_fclr_sel
BEGIN
	main.cl_fclr.hide
	main.cl_fclr.show
	main.cl_fclr.hide
END

LIST orientation_contents
BEGIN
	resource LBL_DEFAULT
	resource LBL_HORIZONTAL
	resource LBL_VERTICAL
END

LIST orientation_values
BEGIN
	""
	horizontal
	vertical
END

PROGRAM command
BEGIN	
	if main.cl_fclr.isvisible() {
		optionarg /quoted main.cl_fclr
	}
	if ! main.cl_fclr.isvisible() {
		optionarg /hidedefault main.cb_fclr
	}
	optionarg main.ed_intensity    
	
	if main.cl_clr.isvisible() {
		optionarg /quoted main.cl_clr
	}
	if ! main.cl_clr.isvisible() {
		optionarg /hidedefault main.cb_clr
	}	
	optionarg /hidedefault main.cb_width
	optionarg /hidedefault main.cb_lpattern	
	
	put " " main.cb_orient
	optionarg main.ed_barwidth
	optionarg main.ed_base
	optionarg /hidedefault main.sp_gap
END
