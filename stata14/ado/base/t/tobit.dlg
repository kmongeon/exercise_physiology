/*
  tobit

*!  VERSION 1.3.1  20may2015

  keyword:  eclass

*/

VERSION 14.0

INCLUDE _std_large
INCLUDE header

HELP hlp1, view("help tobit")
RESET res1

SCRIPT PREINIT
BEGIN
	script se_createAsSvyInternalML
	program svy_check_title
	setscale_hadj resource TOBIT_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script max_setDefaultNoLog
	script se_setJknifeBstrap_on
	script sub_set_by_on
	program se_setFinalInitState
END

SCRIPT svy_is_on
BEGIN
	//
END
SCRIPT svy_is_off
BEGIN
	//
END
PROGRAM svy_check_title
BEGIN
	if __MESSAGE.contains("-svy-") {
		call settitle resource TOBIT_DLG_SVY_TITLE
	}
END

DIALOG main, 								///
	title(resource TOBIT_DLG_TITLE) /* has svy title */		///
	tabtitle(resource LBL_TAB_MODEL)
BEGIN
  TEXT tx_dv 		_lft	_top	_vnwd	.,			///
	label(resource LBL_DEPVAR)
  VARNAME vn_dv		@ 	_ss	@	.,			///
	ts								///
	numeric								///
	label(resource LBL_DEPVAR)

  TEXT tx_iv		_vlx	_top	160	.,			///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_iv		@	_ss	_vlwd	.,			///
	fv ts								///
	allowcat							///
	numeric								///
	label(resource LBL_INDDEPVAR)
  CHECKBOX ck_nocons	@	_ms	@	.,			///
	label(resource LBL_NOCONSTANT)					///
	option(noconstant)

  GROUPBOX gb_censor _lft      +35      _iwd      _ht8h,		/*
		*/ label(resource LBL_SPECIFY_AT_LEAST_ONE_CENSORING_LIMIT) /*
		*/
  RADIO    rb_noll   _indent   _ss       _cwd2     .,			/*
	  	*/ first						/*
	  	*/ label(resource LBL_NO_LEFT_CENSORING_LIMIT)		/*
		*/ onclickon("gaction main.en_ll.disable")
  DEFINE y @y
  RADIO    rb_ll     @         _ss       @         .,			/*
		*/ label(resource LBL_LEFT_CENSORING_AT_MINIMUM)	/*
		*/ clickon("gaction main.en_ll.disable")		/*
		*/ option("ll")	default(1)				/*
		*/
  RADIO    rb_llarg  @         _ss       @         .,			/*
		*/ last							/*
		*/ label(resource LBL_SPECIFIED_LEFT_CENSORING_LIMIT)	/*
		*/ clickon("gaction main.en_ll.enable")			/*
		*/
  EDIT     en_ll     _indent2  _ss       _en14wd   .,			/*
		*/ label(resource LBL_LEFT_CENSORING_LIMIT)		/*
		*/ numonly						/*
		*/ option("ll")						/*
		*/
  RADIO    rb_noul   _lft2     y         _cwd2     .,			/*
		*/ first						/*
		*/ onclickon("gaction main.en_ul.disable")		/*
		*/ label(resource LBL_NO_RIGHT_CENSORING_LIMIT)
  RADIO    rb_ul     @         _ss       @         .,			/*
		*/ label(resource LBL_RIGHT_CENSORING_AT_MAXIMUM)	/*
		*/ clickon("gaction main.en_ul.disable")		/*
		*/ option("ul")						/*
		*/
  RADIO    rb_ularg  @         _ss       @         .,			/*
		*/ last							/*
		*/ label(resource LBL_SPECIFIED_RIGHT_CENSORING_LIMIT)	/*
		*/ clickon("gaction main.en_ul.enable")			/*
		*/
  EDIT     en_ul     _indent2  _ss       _en14wd   .,			/*
		*/ label(resource LBL_RIGHT_CENSORING_LIMIT)		/*
		*/ numonly						/*
		*/ option("ul")						/*
		*/


  TEXT     tx_offset _lft      _xxls     _ibwd     .,			/*
		*/ label(resource LBL_OFFSET_VARIABLE_OPTIONAL)		/*
		*/
  VARNAME  vn_offset @         _ss       _vnwd     .,			/*
		*/ label(resource LBL_OFFSET_VARIABLE)			/*
		*/ option("offset")					/*
		*/ numeric						/*
		*/
END

INCLUDE sub_by_ifin_over_subpop
INCLUDE weights_fpai
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
        program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2b
  DEFINE _y 20
  INCLUDE _sp_level

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	INCLUDE _report_columns_pr		
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
END

INCLUDE max_internal

PROGRAM command
BEGIN
	put /program by_output " "
	put /program se_prefix_output " "
	put "tobit "
	varlist main.vn_dv [main.vl_iv]
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		option main.ck_nocons
		if main.rb_noll & main.rb_noul {
			call create STRING _MSG_TMP
                        call _MSG_TMP.format				/// 
				"resource LBL_MSGBOX_AT_LEAST_ONE_CENSORING_LIMIT_SPEC_FMT" ///
				"resource LBL_TAB_MODEL"
			stopbox stop "class _MSG_TMP.value"
// On the "Model" tab, at least one censoring limit must be specified.
		}
		if main.rb_llarg {
			require main.en_ll
			optionarg main.en_ll
		}
		if main.rb_ularg {
			require main.en_ul
			optionarg main.en_ul
		}
		option main.rb_ll
		option main.rb_ul
		optionarg main.vn_offset
		put " " /program se_output
		put " " /program rpt_output
		put " " /program max_output
	endoptions
END
