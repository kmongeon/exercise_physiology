/*
    bayestest_interval_intspec.dlg

*!  VERSION 1.1.5  17may2016

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 290
INCLUDE header

HELP hlp1, view("help bayestest interval")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource BAYESTEST_INTERVAL_INTSPEC_DLG_HSCALE

	create ARRAY paras_list
	create STRING paras_str
	create STRING paras_tmp
	
	create STRING cmdName
	create STRING cmdStr

	create STRING dlgname
	dlgname.storeDialogClassName
	
	cmdStr.setvalue ""
	cmdName.setvalue ""
	cmdName.setvalue e(cmd)
	cmdStr.setvalue e(cmdline)
	
	program paras_init
END

PROGRAM paras_init
BEGIN
	call paras_list.Arrdropall

	if cmdName.equals("bayesmh") & !cmdStr.equals("") {
		put "bayesmh_parse_params "
		put cmdStr
		stata hidden immediate
		put "bayesmh_wrk_dlg " 
		put dlgname
		put " paras_list"
		put `" "s(params)""'
		put " 0"
		stata hidden immediate
	}
	
	call main.cb_paras.repopulate
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
	title(resource BAYESTEST_INTERVAL_INTSPEC_TITLE)				
BEGIN
  GROUPBOX gb_intspec		_lft	_top	_iwd	_ht23h,		///
	label(resource BAYES_LBL_GB_INTSPEC)
	
  RADIO  rb_onepara		_ilft	_ss	_ibwd	.,		///
	first								///
	onclickon(script main_onepara_on)				///
	label(resource BAYES_LBL_RB_ONEPARA)
  RADIO rb_paraexp		@	80	@	.,		///
	last								///
	onclickon(script main_paraexp_on)				///
	label(resource BAYES_LBL_RB_PARAEXP)
  HLINK hl_exspec		415	80	65	.,		///
	right								///
	onpush(view helpbayesmh_postestimation##functions)		///
	label(resource LBL_PAREN_EXAMPLES_PAREN)
	
  
  COMBOBOX  cb_paras		40	50	200	.,		///
	dropdown							///
	contents(paras_list)						///
	label(resource BAYES_LBL_RB_ONEPARA)
	
  EXP   ex_exspec		@	100	440	.,		///
	parameter							///
	bayes 								///
	tooltip(resource BAYES_LBL_EX_EXSPEC_TIP)			///
	error(resource BAYES_LBL_RB_PARAEXP)				///
	label(resource LBL_BU_CREATE)

  TEXT  tx_intvalues		_ilft	+40	@	.,		///
	label(resource BAYES_LBL_TX_INTERVAL_TO_TEST)
  RADIO rb_int			@	_ms	@	.,		///
	first								///
	onclickon(script main_int_enable)				///
	label(resource BAYES_LBL_RB_INTERVAL_VALUES)
  RADIO  rb_single		_ilft	220	_ibwd	.,		///
	last								///
	onclickon(script main_int_disable)				///
	label(resource BAYES_LBL_RB_SINGLE_VALUE)
	
  TEXT  tx_lower		40 	190	50	.,		///
	label(resource BAYES_LBL_TX_LOWER)
  EDIT  ed_lower		75	@	_en7wd	.,		///
	numonly								///
	default(.)							///
	label(resource BAYES_LBL_TX_LOWER)
	
  COMBOBOX cb_lower		160	@	50	.,		///
	dropdownlist							///
	contents(main_direct_list)
  TEXT  tx_theta		210 	@	90	.,		///
	center								///
	label(resource BAYES_LBL_TX_THETA)
  COMBOBOX cb_upper		300	@	50	.,		///
	dropdownlist							///
	contents(main_direct_list)
	
  EDIT  ed_upper		370	@	_en7wd	.,		///
	numonly								///
	default(.)							///
	label(resource BAYES_LBL_TX_UPPER)
  TEXT  tx_upper		_en7sep @	50	.,		///
	label(resource BAYES_LBL_TX_UPPER)
	
  TEXT  tx_equal		40 	245	200	.,		///
	label(resource BAYES_LBL_TX_EQUALS)
  EDIT  ed_equal		75	@	_en7wd	.,		///
	numonly								///
	label(resource BAYES_LBL_TX_EQUALS)
END

LIST main_direct_list
BEGIN	
	<
	<=
END

SCRIPT main_onepara_on
BEGIN
	main.cb_paras.enable
	main.ex_exspec.disable
END 

SCRIPT main_paraexp_on
BEGIN
	main.cb_paras.disable
	main.ex_exspec.enable
END

SCRIPT main_int_disable
BEGIN
	main.tx_equal.enable
	main.ed_equal.enable
	main.tx_lower.disable
	main.ed_lower.disable
	main.cb_lower.disable
	main.tx_theta.disable
	main.cb_upper.disable
	main.ed_upper.disable
	main.tx_upper.disable
END

SCRIPT main_int_enable
BEGIN
	main.tx_equal.disable
	main.ed_equal.disable
	main.tx_lower.enable
	main.ed_lower.enable
	main.cb_lower.enable
	main.tx_theta.enable
	main.cb_upper.enable
	main.ed_upper.enable
	main.tx_upper.enable
END

PROGRAM command
BEGIN
	put "("
	if main.rb_onepara {
		require main.cb_paras
		put main.cb_paras
	}
	else {
		require main.ex_exspec
		put main.ex_exspec
	}
	if main.rb_single {
		require main.ed_equal
		if main.ed_equal {
			put " == "
			put main.ed_equal
		}
		put ")"
	}
	beginoptions
		if main.rb_int {
			call create DOUBLE lvalue
			call create DOUBLE uvalue
			call lvalue.setvalue class main.ed_lower.value
			call uvalue.setvalue class main.ed_upper.value
			if main.ed_lower.equals("") & main.ed_upper.equals("") {
		stopbox stop resource BAYES_MSGBOX_ONE_UPPER_LOWER_SPECIFIED
// At least one of lower value or upper value must be specified.
			}
			if main.ed_lower & main.ed_upper {
				if main.ed_lower.iseq(".") & main.ed_upper.iseq(".") {
		stopbox stop resource BAYES_MSGBOX_ONE_UPPER_LOWER_SPECIFIED
// At least one of lower value or upper value must be specified.		
				}
				if main.ed_lower.isneq(".") & main.ed_upper.isneq(".") { 
					if lvalue.isNumlistGE(uvalue) {
		stopbox stop resource BAYES_MSGBOX_LOWER_MUST_BE_LESSTHAN_UPPER
// Lower value must be less than upper value.
					}
				}
			}
			if main.ed_lower & main.ed_lower.isneq(".") {
				put " lower("
				put main.ed_lower
				if main.cb_lower.equals("<=") {
					put ", inclusive"
				}
				put ")"
			}
			if main.ed_upper & main.ed_upper.isneq(".") {
				put " upper("
				put main.ed_upper 
				if main.cb_upper.equals("<=") {
					put ", inclusive"
				}
				put ")"
			}
			put ")"
		}
	endoptions
END

