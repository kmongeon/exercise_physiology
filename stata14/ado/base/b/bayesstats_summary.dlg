/*
    bayesstats summary

*!  VERSION 1.0.6  03jun2015

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 470
INCLUDE header

HELP hlp1, view("help bayesstats summary")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource BAYESSTATS_SUMMARY_DLG_HSCALE

	create STRING sTMP
	create STRING main_build_tmp
	create STRING main_build_tmp2
	create STRING main_build_childdlg
	create STRING main_build_child_result
	create DOUBLE main_build_position
	
	create ARRAY main_paraspecsum_list
	create SVECTOR mainParaspecCommandDisplayList
	create SVECTOR mainParaspecCommandList
	create SVECTOR mainParaspecCommandValues
	create SVECTOR mainParaspecStatusList
	create SVECTOR mainParaspecChildDialogList
	create DOUBLE main_paraspec_count
	create DOUBLE main_paraspec_arraysize
	create STRING main_paraspec_output
	
	create BOOLEAN hasbatch
	
	main.bu_paraspec_edit.disable
	main.bu_paraspec_disable.disable
	main.bu_paraspec_enable.disable
END

SCRIPT POSTINIT
BEGIN
	mainParaspecCommandDisplayList.copyToArray main_paraspecsum_list
	main.lb_paraspecBuild.repopulate
	
	create STRING bu_facvarsResults
        program bu_facvars_ckResults
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
	title(resource BAYESSTATS_SUMMARY_TITLE)				
BEGIN
  GROUPBOX gb_syntax		_lft	_top	_iwd	_ht19h,		///
	label(resource LBL_SYNTAX)
  RADIO  rb_all			_ilft	_ss	_ibwd	.,		///
	first								///
	onclickon(script main_parasyntax_disable)			///
	label(resource BAYESSTATS_SUMMARY_MAIN_RB_ALL)
  RADIO rb_selected		@	_ss	@	.,		///
	last								///
	onclickon(script main_parasyntax_enable)			///
	label(resource BAYESSTATS_SUMMARY_MAIN_RB_SELECTED)
	
  GROUPBOX gb_paraspec		_iilft	+20	_iibwd	_ht12,		///
	label("")
  LISTBOX lb_paraspecBuild	_indent	_ms	245	95,		///
	contents(main_paraspecsum_list)					///
	ondblclick(program main_paraspec_edit)				///
	onselchange(program main_paraspec_sel_changed)			///
	nomemory
  BUTTON bu_paraspec_create	295	@	80	.,		///
	label(resource LBL_BU_CREATE)					///
	onpush(program main_paraspec_create)
  BUTTON bu_paraspec_edit	@	_ms	@	.,		///
	label(resource LBL_BU_EDIT)					///
	onpush(program main_paraspec_edit)
  BUTTON bu_paraspec_disable	@	_ms	@	.,		///
	label(resource LBL_BU_DISABLE)					///
	onpush(program main_paraspec_disable)
  BUTTON bu_paraspec_enable	@	_ms	@	.,		///
	label(resource LBL_BU_ENABLE)					///
	onpush(program main_paraspec_enable)
  TEXTBOX tb_paraspec_note	40	+25	370	20,		///
	label(resource LBL_PRESS_CREATE_DEFINE_SPECIFICATION)

  GROUPBOX gb_opt		_lft	+55	_iwd	_ht17h,		///
	label(resource LBL_OPTIONS)
  COMBOBOX  sp_level		_ilft	_ms	_en14wd	.,		///
	dropdown							///
	label(resource BAYES_LBL_TX_LEVEL)				///
	default(c(level))						///
	contents(clevels)						///
	option(clevel)
  TEXT	    tx_level		_en14sep @	300	.,		///
	label(resource BAYES_LBL_TX_LEVEL)
  CHECKBOX ck_hpd		_ilft	_ls	_ibwd	.,		///
	option(hpd)							///
	label(resource BAYES_LBL_CK_HPD)
  EDIT    ed_batch		_ilft	_ls	_en14wd	.,		///
	numonly								///
	option(batch)							///
	default(0)							///
	onchange(program main_batch_change)				///
	label(resource BAYES_LBL_TX_BATCH)
  TEXT	  tx_batch		_en14sep @	300	.,		///
	label(resource BAYES_LBL_TX_BATCH)
  SPINNER  sp_skip		_ilft	_ls	_en14wd	.,		///
	option(skip)							///
	min(0) max(e(mcmcsize))						///
	default(0)
  TEXT 	   tx_skip		_en14sep @	300	.,		///
	label(resource BAYES_LBL_TX_SKIP)
  CHECKBOX ck_nolegend		_ilft	_ls	_ibwd	.,		///
	option(nolegend)						///
	label(resource LBL_SUP_TABLE_LEGEND)

  BUTTON bu_facvars		_ilft	_ls	200	.,		///
	onpush("program bu_facvars_getOpts")				///
	label(resource FACTOR_VARS_REPORTING_DLG_TITLE)
END

PROGRAM bu_facvars_getOpts
BEGIN
	call create CHILD factor_vars_reporting AS bu_facvars
	call bu_facvars.setExitString bu_facvarsResults
	call bu_facvars.setExitAction "program bu_facvars_ckResults"
END

PROGRAM bu_facvars_ckResults
BEGIN
	if bu_facvarsResults {
		call main.bu_facvars.formatlabel 			///
			"resource _BU_FACTOR_VARS_REPORTING_BU_FACVARS_DSPOPS_FMT" " *"
	}
	else {
		call main.bu_facvars.formatlabel 	 		///
			"resource _BU_FACTOR_VARS_REPORTING_BU_FACVARS_DSPOPS_FMT" "  "
	}
END

LIST clevels
BEGIN
	99.9 
	99.5
	99
	97.5
	95
	90
	80
END

PROGRAM main_batch_change
BEGIN
	if main.ed_batch & main.ed_batch.isneq(0) {
		call adv.tx_corrlag.disable
		call adv.sp_corrlag.disable
		call adv.tx_corrtol.disable
		call adv.ed_corrtol.disable
	}
	else {
		call adv.tx_corrlag.enable
		call adv.sp_corrlag.enable
		call adv.tx_corrtol.enable
		call adv.ed_corrtol.enable
	}
END

SCRIPT main_parasyntax_enable
BEGIN
	main.gb_paraspec.enable
	main.lb_paraspecBuild.enable
	main.bu_paraspec_create.enable
	main.bu_paraspec_edit.disable
	main.bu_paraspec_disable.disable
	main.bu_paraspec_enable.disable
	main.tb_paraspec_note.enable
	program main_paraspec_sel_changed
END

SCRIPT main_parasyntax_disable
BEGIN
	main.gb_paraspec.disable
	main.lb_paraspecBuild.disable
	main.bu_paraspec_create.disable
	main.bu_paraspec_edit.disable
	main.bu_paraspec_disable.disable
	main.bu_paraspec_enable.disable
	main.tb_paraspec_note.disable
END

PROGRAM main_paraspec_create
BEGIN
	call main_paraspec_count.setvalue class mainParaspecChildDialogList.size
	call main_paraspec_count.increment
	call main_build_tmp.setvalue "main_paraspec_"
	call main_build_tmp.append class main_paraspec_count.value
	call main_build_tmp.withvalue main_build_childdlg.setvalue @
	call program main_show_paraspec_child
END

PROGRAM main_paraspec_edit
BEGIN
	call mainParaspecCommandDisplayList.findstr class		///
		main.lb_paraspecBuild.value
	call main_build_position.setvalue class				///
		mainParaspecCommandDisplayList.position
	call main_build_position.withvalue				///
		main_build_childdlg.setvalue				///
		class mainParaspecChildDialogList.values[@]
	call program main_show_paraspec_child_edit
END

PROGRAM main_show_paraspec_child
BEGIN
	call main_build_tmp.setvalue "bayesstats_summary_paraspec AS "
	call main_build_tmp.append class main_build_childdlg.value
	call main_build_tmp.withvalue create CHILD @
	call main_build_childdlg.withvalue @.setExitString		///
		"main_build_child_result"
	call main_build_childdlg.withvalue @.setExitAction		///
		"program main_paraspec_child_create_done"

	call main_paraspec_count.withvalue				///
		sTMP.format "resource LBL_SPECIFICATION_FMT" "@" ""

	call main_build_childdlg.withvalue				///
		main_build_tmp2.setvalue "@.settitle"
	call main_build_tmp2.append `" ""'
	call main_build_tmp2.append class sTMP.value
	call main_build_tmp2.append `"""'
	call main_build_tmp2.withvalue @
END

PROGRAM main_show_paraspec_child_edit
BEGIN
	call main_build_tmp.setvalue "bayesstats_summary_paraspec AS "
	call main_build_tmp.append class main_build_childdlg.value
	call main_build_tmp.withvalue create CHILD @
	call main_build_childdlg.withvalue @.setExitString		///
		"main_build_child_result"
	call main_build_childdlg.withvalue @.setExitAction		///
		"program main_paraspec_child_edit_done"

	call main_build_position.withvalue				///
		main_build_tmp.setvalue					///
		class mainParaspecCommandDisplayList.values[@]
	call main_build_childdlg.withvalue				///
		main_build_tmp2.setvalue "@.settitle"
	call main_build_tmp2.append `" ""'
	call main_build_tmp2.append class main_build_tmp.value
	call main_build_tmp2.append `"""'
	call main_build_tmp2.withvalue @
END

PROGRAM main_paraspec_child_create_done
BEGIN
	call main_paraspec_arraysize.setvalue class			///
		mainParaspecChildDialogList.size
	call main_paraspec_arraysize.increment

	/* Store child dialog name */
	call mainParaspecChildDialogList.store class			///
		main_paraspec_arraysize.value class main_build_childdlg.value

	call main_paraspec_arraysize.withvalue				///
		sTMP.format "resource LBL_SPECIFICATION_FMT" "@" ""

	/* Store listbox display equation string */
	call sTMP.withvalue						///
		mainParaspecCommandDisplayList.store class		///
		main_paraspec_arraysize.value "@"

	/* Store child dialog result string */
	call mainParaspecCommandList.store class			///
		main_paraspec_arraysize.value				///
		class main_build_child_result.value


	/* Store child dialog result string for final command
	   For disable/enable */
	call mainParaspecCommandValues.store class			///
		main_paraspec_arraysize.value				///
		class main_build_child_result.value

	/* Store disable/enable status */
	call mainParaspecStatusList.store class				///
		main_paraspec_arraysize.value "active"

	call mainParaspecCommandDisplayList.copyToArray main_paraspecsum_list
	call main.lb_paraspecBuild.repopulate
	call sTMP.withvalue						///
		main.lb_paraspecBuild.setvalue "@"
END

PROGRAM main_paraspec_child_edit_done
BEGIN
	call mainParaspecChildDialogList.findstr class			///
		main_build_childdlg.value
	call main_build_position.setvalue class				///
		mainParaspecChildDialogList.position
	call main_build_position.withvalue main_build_tmp.setvalue	///
		class mainParaspecStatusList.values[@]
	if main_build_tmp.iseq("active") {
	
		call main_build_position.withvalue			///
			sTMP.format "resource LBL_SPECIFICATION_FMT" "@" ""
	
		call sTMP.withvalue					///
			mainParaspecCommandDisplayList.store class	///
			main_build_position.value "@"

		call mainParaspecCommandValues.store class		///
			main_build_position.value class			///
			main_build_child_result.value

		call sTMP.withvalue					///
			main_build_tmp2.setvalue "@"
	}
	else {
		call main_build_position.withvalue			///
			sTMP.format "resource LBL_SPECIFICATION_FMT" "@" "resource LBL_PAREN_DISABLED"

		call sTMP.withvalue					///
			mainParaspecCommandDisplayList.store		///
			class main_build_position.value "@"
			
		call sTMP.withvalue					///
			main_build_tmp2.setvalue "@"
	}

	call mainParaspecCommandList.store class			///
		main_build_position.value class				///
		main_build_child_result.value

	call mainParaspecCommandDisplayList.copyToArray main_paraspecsum_list
	call main.lb_paraspecBuild.repopulate
	call main.lb_paraspecBuild.setvalue class main_build_tmp2.value
END

PROGRAM main_paraspec_sel_changed
BEGIN
	call mainParaspecCommandDisplayList.findstr class		///
		main.lb_paraspecBuild.value
	call main_build_position.setvalue class				///
		mainParaspecCommandDisplayList.position
	call main_build_position.withvalue main_build_tmp.setvalue	///
		class mainParaspecStatusList.values[@]
	if main_build_position {
		call main.bu_paraspec_edit.enable

		call main_build_position.withvalue			///
			main.tb_paraspec_note.setlabel			///
		class mainParaspecCommandList.values[@]
	}

	if main_build_tmp.iseq("disable") & main.lb_paraspecBuild.isenabled() {
		call main.bu_paraspec_enable.enable
		call main.bu_paraspec_disable.disable
	}
	if main_build_tmp.iseq("active") & main.lb_paraspecBuild.isenabled() {
		call main.bu_paraspec_enable.disable
		call main.bu_paraspec_disable.enable
	}
END

PROGRAM main_paraspec_disable
BEGIN
	call mainParaspecCommandDisplayList.findstr class		///
		main.lb_paraspecBuild.value
	call main_build_position.setvalue class				///
		mainParaspecCommandDisplayList.position

	call main_build_tmp.setvalue class main.lb_paraspecBuild.value
	call mainParaspecStatusList.store class				///
		main_build_position.value "disable"
	call mainParaspecCommandValues.store class main_build_position.value ""

	call main_build_position.withvalue				///
		sTMP.format "resource LBL_SPECIFICATION_FMT" "@" "resource LBL_PAREN_DISABLED"

	call sTMP.withvalue						///
		mainParaspecCommandDisplayList.store			///
		class main_build_position.value "@"

	call mainParaspecCommandDisplayList.copyToArray main_paraspecsum_list
	call sTMP.withvalue main_build_tmp2.setvalue "@"
	
	call main.lb_paraspecBuild.repopulate
	call main.lb_paraspecBuild.setvalue class main_build_tmp2.value
END

PROGRAM main_paraspec_enable
BEGIN
	call mainParaspecCommandDisplayList.findstr class		///
		main.lb_paraspecBuild.value
	call main_build_position.setvalue class				///
		mainParaspecCommandDisplayList.position

	call mainParaspecStatusList.store class				///
		main_build_position.value "active"
	call main_build_position.withvalue				///
		mainParaspecCommandValues.store				///
		class main_build_position.value class			///
		mainParaspecCommandList.values[@]

	call main_build_position.withvalue				///
		sTMP.format "resource LBL_SPECIFICATION_FMT" "@" ""

	call sTMP.withvalue						///
		mainParaspecCommandDisplayList.store			///
		class main_build_position.value "@"

	call mainParaspecCommandDisplayList.copyToArray main_paraspecsum_list

	call main_build_position.withvalue				///
		main_build_tmp2.setvalue class				///
		mainParaspecCommandDisplayList.values[@]
	call main.lb_paraspecBuild.repopulate
	call main.lb_paraspecBuild.setvalue class main_build_tmp2.value
END

DIALOG adv, tabtitle(resource LBL_ADVANCED)
BEGIN
  SPINNER  sp_corrlag		_lft	_top	_en14wd	.,		///
	option(corrlag)							///
	min(1) max(1000000)						///
	default(500)							///
	onchange(program adv_change)					///
	label(resource BAYES_LBL_TX_MAX_CORRLAG)
  TEXT 	   tx_corrlag		_en14sep @	350	.,		///
	label(resource BAYES_LBL_TX_MAX_CORRLAG)
  EDIT     ed_corrtol		_lft	_ls	_en14wd	.,		///
	numonly								///
	option(corrtol)							///
	default(0.01)							///
	onchange(program adv_change)					///
	label(resource BAYES_LBL_TX_CORRTOL)
  TEXT 	   tx_corrtol		_en14sep @	350	.,		///
	label(resource BAYES_LBL_TX_CORRTOL)
END

PROGRAM adv_change
BEGIN
	call hasbatch.settrue
	
	if adv.sp_corrlag & adv.sp_corrlag.isneq(500) {
		call hasbatch.setfalse
	}
	
	if adv.ed_corrtol & adv.ed_corrtol.isneq(0.01) {
		call hasbatch.setfalse
	}
	
	if hasbatch {
		call main.tx_batch.enable
		call main.ed_batch.enable
	}
	else {
		call main.tx_batch.disable
		call main.ed_batch.disable
	}
END

PROGRAM advanced_output
BEGIN
	optionarg /hidedefault adv.sp_corrlag
	optionarg /hidedefault adv.ed_corrtol
END

PROGRAM command
BEGIN
	put "bayesstats summary"
	if main.rb_all {
		put " _all"
	}
	if main.rb_selected {
		call mainParaspecCommandValues.copyToString main_paraspec_output
		if main_paraspec_output.equals("") {
			stopbox stop resource BAYES_MSGBOX_PARAMETER_SPEC_REQUIRED
// Parameter specifications must be specified.
		}
		put " " main_paraspec_output	
	}
	beginoptions
		optionarg /hidedefault main.sp_level
		option main.ck_hpd
		optionarg /hidedefault main.ed_batch
		optionarg /hidedefault main.sp_skip
		option main.ck_nolegend
		put " " bu_facvarsResults
		put " " /program advanced_output
	endoptions
END

