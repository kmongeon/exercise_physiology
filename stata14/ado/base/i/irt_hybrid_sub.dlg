/*
  irt_hybrid_sub

*!  VERSION 1.1.1  21apr2015

*/

VERSION 14.0

SCRIPT PREINIT
BEGIN
        setscale_hadj resource IRT_HYBRID_DLG_HSCALE
END

INCLUDE _std_medium
POSITION . . 420 380
HELP hlp1, view("help irt hybrid")
RESET res1
OK ok1, label(resource IRT_HYBRID_OK)
CANCEL can1, label(resource LBL_BU_CANCEL)

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	label(resource IRT_HYBRID_DLG_TITLE)
BEGIN
  GROUPBOX gb_model		_lft	_top	_iwd	355,		///
	label(resource LBL_MODELS)
  TEXT	tx_bmodel		_indent	_ms	_ibwd	.,		///
	label(resource IRT_HYBRID_MAIN_TX_BMODEL)
  TEXT	tx_omodel		@	120	_ibwd	.,		///
	label(resource IRT_HYBRID_MAIN_TX_OMODEL)
  TEXT	tx_umodel		@	225	_ibwd	.,		///
	label(resource IRT_HYBRID_MAIN_TX_UMODEL)
  RADIO rb_pl1			65	55	_ibwd	., 		///
	first								///
	onclickon(program model_choose_change)				///
	label(resource IRT_HYBRID_MAIN_RB_PL1)
  RADIO rb_pl2			@	75	300	., 		///
	onclickon(program model_choose_change)				///
	label(resource IRT_HYBRID_MAIN_RB_PL2)
  RADIO rb_pl3			@	95	300	., 		///
	onclickon(program model_choose_change)				///
	label(resource IRT_HYBRID_MAIN_RB_PL3)
  RADIO rb_grm			65	140	300	., 		///
	onclickon(program model_choose_change)				///
	label(resource IRT_HYBRID_MAIN_RB_GRM)
  RADIO rb_pcm			@	160	300	., 		///
	onclickon(program model_choose_change)				///
	label(resource IRT_HYBRID_MAIN_RB_PCM)
  RADIO rb_gpcm			@	180	300	., 		///
	onclickon(program model_choose_change)				///
	label(resource IRT_HYBRID_MAIN_RB_GPCM)
  RADIO rb_rsm			@	200	300	., 		///
	onclickon(program model_choose_change)				///
	label(resource IRT_HYBRID_MAIN_RB_RSM)
  RADIO rb_nrm			65	250	300	., 		///
	last								///
	onclickon(program model_choose_change)				///
	label(resource IRT_HYBRID_MAIN_RB_NRM)
	
  TEXT	tx_eitem		20	285	380	.,		///
	label(resource IRT_HYBRID_MAIN_TX_EITEM)
  VARLIST vl_eitem		@	305	380	.,		///
	label(resource IRT_HYBRID_MAIN_TX_EITEM)
  CHECKBOX ck_sepguessing	@	_ls	@	.,		///
	option(sepguessing)						///
	label(resource IRT_HYBRID_MAIN_CK_SEPGUESSING)
END

PROGRAM model_choose_change
BEGIN
	if main.rb_pl3 {
		call main.ck_sepguessing.show
	}
	else {
		call main.ck_sepguessing.hide
	}
END

PROGRAM	command
BEGIN
	require main.vl_eitem
	put "("
	if main.rb_pl1 {
		put "1pl "
	}
	if main.rb_pl2 {
		put "2pl "
	}
	if main.rb_pl3 {
		put "3pl "
	}
	if main.rb_grm {
		put "grm "
	}
	if main.rb_pcm {
		put "pcm "
	}
	if main.rb_gpcm {
		put "gpcm "
	}
	if main.rb_rsm {
		put "rsm "
	}
	if main.rb_nrm {
		put "nrm "
	}
	put main.vl_eitem
	if main.ck_sepguessing.isvisible() & main.ck_sepguessing {
		put ", sepguessing"
	}
	put ")"
END
