/*
	import delimited range

*!  VERSION 1.1.1  21apr2015

	Used by import_delimited_alt.dlg 
	THIS DIALOG IS ONLY USED ON SOLARIS PLATFORMS. (deprecated)
*/

VERSION 14.0

INCLUDE _std_medium
DEFINE _dlght 110
INCLUDE header

RESET res1

SCRIPT range_PREINIT
BEGIN
	create BOOLEAN brow
	create BOOLEAN bcol
	
	brow.setfalse
	bcol.setfalse
END


DIALOG range, label("Set row and column ranges")
BEGIN
	GROUPBOX gb_row		_lft	_top	_cwd1	_ht5h,		///
		label("Row")
	DEFINE spwd 120
	CHECKBOX ck_rowfirst    _indent _ss	40	.,		///
		onclickon(range.sp_rowfirst.enable)			///
		onclickoff(range.sp_rowfirst.disable)			///
		label("First:")
	SPINNER  sp_rowfirst	65	@	spwd	.,		///
		default(0) min(0)
	CHECKBOX ck_rowlast     20 	_ls	40	.,		///
		onclickon(range.sp_rowlast.enable)			///
		onclickoff(range.sp_rowlast.disable)			///
		label("Last:")
	SPINNER  sp_rowlast	65	@	spwd	.,		///
		default(0) min(0)
	GROUPBOX gb_col		_lft2	_top	_cwd1	_ht5h,		///
		label("Column")
	CHECKBOX ck_colfirst    _indent _ss	40	.,		///
		onclickon(range.sp_colfirst.enable)			///
		onclickoff(range.sp_colfirst.disable)			///
		label("First:")
	SPINNER  sp_colfirst	275	@	spwd	.,		///
		default(0) min(0)
	CHECKBOX ck_collast     230 	_ls	40	.,		///
		onclickon(range.sp_collast.enable)			///
		onclickoff(range.sp_collast.disable)			///
		label("Last:")
	SPINNER  sp_collast	275	@	spwd	.,		///
		default(0) min(0)	
END

PROGRAM command
BEGIN
	if range.ck_rowfirst & range.sp_rowfirst.isneq(0) {
		call brow.settrue
	}
	if range.ck_rowlast & range.sp_rowlast.isneq(0) {
		call brow.settrue
	}
	if brow {
		put "rowrange("
		if range.ck_rowfirst & range.sp_rowfirst.isneq(0) {
			put range.sp_rowfirst
		}
		if range.ck_rowlast & range.sp_rowlast.isneq(0) {
		   	put ":"
		   	put range.sp_rowlast
		}
		put ")"
	}
	if range.ck_colfirst & range.sp_colfirst.isneq(0) {
			call bcol.settrue
	}
	if range.ck_collast & range.sp_collast.isneq(0) {
			call bcol.settrue
	}
	if bcol {
		put " "
		put "colrange("
		if range.ck_colfirst & range.sp_colfirst.isneq(0) {
			put range.sp_colfirst
		}
		if range.ck_collast & range.sp_collast.isneq(0) {
			put ":"
			put range.sp_collast
		}
		put ")"
	}
END
