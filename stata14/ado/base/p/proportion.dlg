/*
  proportion.dlg

*!  VERSION 1.3.1  20may2015

  keyword:  eclass

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 300
INCLUDE header

HELP hlp1, view("help proportion")
RESET res1

SCRIPT PREINIT
BEGIN
	program parseMessage
	script se_createAsSvyAnalytic
	program svy_check_title
	setscale_hadj resource PROPORTION_DLG_HSCALE
END

PROGRAM parseMessage
BEGIN
	if __MESSAGE.contains("__MI__") {
		call script se_setMI_on
	}
END

SCRIPT POSTINIT
BEGIN
	script se_setTitleSeCluster
	script sub_set_over_on
	program se_setFinalInitState
END

SCRIPT svy_is_on
BEGIN
	//
END
SCRIPT svy_is_off
BEGIN
	//
END
PROGRAM svy_check_title
BEGIN
	if __MESSAGE.contains("-svy-") {
		call settitle resource PROPORTION_SVY_DLG_TITLE
	}
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	title(resource PROPORTION_DLG_TITLE) // has svy title
BEGIN
  TEXT     tx_vars   _lft      _top      _cwd1     .,			///
		label(resource LBL_VARIABLES)
  VARLIST  vl_vars   _lft      _ss       _iwd      .,			///
  		numeric							///
		label(resource LBL_VARIABLES)

  INCLUDE svy_stdize

  CHECKBOX ck_nolabel  _lft    +45       _iwd      .,			///
		label(resource LBL_SUPPRESS_VALUE_LABELS)		///
		option(nolabel)
  CHECKBOX ck_missing   @      _ms       @         .,			///
		label(resource LBL_TREAT_MISSING_VALUES_LIKE_OTHER_VALUES) ///
		clickon(program check_legend)				///
		clickoff(program check_legend)				///
		option(missing)
END

INCLUDE svy_stdize_pr

INCLUDE sub_by_ifin_over_subpop
INCLUDE weights_fpi
INCLUDE se

SCRIPT rpt_PREINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2b
  DEFINE _y _top
  INCLUDE _sp_level

  GROUPBOX gb_citype		_lft	_ls	_iwd	_ht4,		///
	label(resource PROPORTION_RPT_GB_CITYPE)
  RADIO rb_citype_logit		_indent	_ss	_inwd	., first	///
	label(resource PROPORTION_RPT_RB_CITYPE_LOGIT)
  RADIO rb_citype_normal	@	_ss	@	., last		///
	option(citype(normal))						///
	label(resource PROPORTION_RB_CITYPE_NORMAL)

  CHECKBOX ck_noheader		_lft	_xls	_iwd	.,		///
	label(resource LBL_SUPPRESS_TABLE_HEADER)			///
	clickon(program check_header)					///
	clickoff(program check_header)					///
	option(noheader)

  CHECKBOX ck_nolegend		@	_ms	@	.,		///
	label(resource LBL_SUP_TABLE_LEGEND)				///
	option(nolegend)

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_coef_table_reporting
END

PROGRAM rpt_bu_fmtcoef_getOpts
BEGIN
	call create CHILD fmt_coef_table_reporting AS rpt_bu_fmtcoef,	///
		message("-cformat_only-")
	call rpt_bu_fmtcoef.setExitString rpt_bu_fmtcoefResults
	call rpt_bu_fmtcoef.setExitAction "program rpt_bu_fmtcoef_ckResults"
END

PROGRAM rpt_bu_fmtcoef_ckResults
BEGIN
	if rpt_bu_fmtcoefResults {
		call rpt.bu_fmtcoef.formatlabel "resource LBL_BU_SET_TABLE_FORMATS_FMT" " *"
	}
	else {
		call rpt.bu_fmtcoef.formatlabel "resource LBL_BU_SET_TABLE_FORMATS_FMT" "  "
	}
END


PROGRAM check_header
BEGIN
	if rpt.ck_noheader {
		call gaction rpt.ck_nolegend.disable
	}
	if !rpt.ck_noheader {
		call gaction rpt.ck_nolegend.enable
	}
END


PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	option rpt.rb_citype_normal
	option rpt.ck_noheader
	option rpt.ck_nolegend
	put " " rpt_bu_fmtcoefResults
END

PROGRAM command
BEGIN
	put /program se_prefix_output " "
	put "proportion "
	varlist main.vl_vars
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		put /program svy_stdize_output
		option main.ck_nolabel
		option main.ck_missing
		put " " /program over_output
		put " " /program se_output
		put " " /program rpt_output
	endoptions
END
