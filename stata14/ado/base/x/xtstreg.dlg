/*
  xtstreg 

*!  VERSION 1.0.4  13feb2015

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 390
INCLUDE header

HELP hlp1, view("help xtstreg")
RESET res1

SCRIPT PREINIT
BEGIN
	program parseMessage
	script se_createAsInternalML
	setscale_hadj resource XTSTREG_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	program check_bytab
	program se_setFinalInitState
END

PROGRAM check_bytab
BEGIN
	if ! __MESSAGE.contains("__MI__") {
		call script sub_set_by_on
	}
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	title(resource XTSTREG_DLG_TITLE) 
BEGIN
  DEFINE _x _xsetbu
  DEFINE _y _top
  INCLUDE _bu_stset

  BUTTON	bu_xtset	240	@	_setbuwd .,		///
	onpush(script show_xtset)					///
	label(resource LBL_BU_PANEL_SETTINGS)

  TEXT tx_iv			_lft	_topph	_cwd1	.,		///
	label(resource LBL_INDDEPVAR)

  VARLIST vl_iv			_lft	_ss	_iwd	.,		///
	numeric								///
	fv ts								///
	label(resource LBL_INDDEPVAR)
  CHECKBOX ck_nocons		@	_ls	@	.,		///
	label(resource LBL_NOCONSTANT) option(noconstant)

  GROUPBOX gb_opts		_lft	_ls	_iwd	_ht25,		///
	label(resource LBL_OPTIONS)

  GROUPBOX gb_model		_ilft	_ss	_ibwd	_ht6,		///
	label(resource LBL_SURVIVAL_DISTRIBUTION)
  RADIO rb_exp			_iilft	_ss	_cwd3	., first	///
	clickon("script main_exp_on")					///
	option("dist(exponential)")					///
	label(resource LBL_EXPONENTIAL)
  DEFINE holdy @y
  RADIO rb_logl			@	_ss	@	.,		///
	clickon("script main_logl_on")					///
	option("dist(loglogistic)")					///
	label(resource LBL_LOGLOGISTIC)
  RADIO rb_weib			@	_ss	@	.,		///
	clickon("script main_weib_on")					///
	option("dist(weibull)")						///
	label(resource LBL_WEIBULL)
  RADIO rb_logn			_lft2	holdy	@	.,		///
	clickon("script main_logn_on")					///
	option("dist(lognormal)")					///
	label(resource LBL_LOGNORMAL)
  RADIO rb_gamma		@	_ss	@	., last		///
	clickon("script main_gamma_on")					///
	option("dist(gamma)")						///
	label(resource LBL_GAMMA_UPPER)

  CHECKBOX ck_time		_ilft	+65	_cwd1	.,		///
	clickon("program time_tr_pr")					///
	clickoff("program time_tr_pr")					///
	option("time")							///
	label(resource LBL_USE_ACCELERATED_FAILURE_TIME_METRIC)
  TEXT tx_offset		@	_ls	@	.,		///
	label(resource LBL_OFFSET_VARIABLE)
  VARNAME  vn_offset		@	_ss	@	.,		///
	option(offset) numeric						///
	label(resource LBL_OFFSET_VARIABLE)

  DEFINE _x _ilft
  DEFINE _y _ls
  DEFINE _cx _ilw80
  DEFINE _bux _islw80
  INCLUDE _constraints

  CHECKBOX ck_collinear		20	_ls	_ibwd	.,		///
	option(collinear)						///
	label(resource LBL_KEEP_COLLINEAR_VARIABLES)
END

PROGRAM time_tr_pr
BEGIN
	if main.rb_logl | main.rb_logn | main.rb_gamma {
		call main.ck_time.disable
		call rpt.ck_tr.enable
	}
	if main.rb_exp | main.rb_weib {
		call main.ck_time.enable
		if main.ck_time {
			call rpt.ck_tr.disable
			call rpt.ck_nohr.disable
		}
		if ! main.ck_time {
			call rpt.ck_tr.disable
			call rpt.ck_nohr.enable
		}
	}
END

SCRIPT show_xtset
BEGIN
	create CHILD xtset
END

PROGRAM main_exp_on
BEGIN
	rpt.ck_nohr.enable
	program time_tr_pr
	script max_from_disable
END

PROGRAM main_weib_on
BEGIN
	rpt.ck_nohr.enable
	program time_tr_pr
	script max_from_enable
END

SCRIPT main_gomp_on
BEGIN
	rpt.ck_nohr.enable
	program time_tr_pr
	script max_from_enable
END

SCRIPT main_logn_on
BEGIN
	rpt.ck_nohr.disable
	program time_tr_pr
	script max_from_enable
END

SCRIPT main_logl_on
BEGIN
	rpt.ck_nohr.disable
	program time_tr_pr
	script max_from_enable
END

SCRIPT main_gamma_on
BEGIN
	rpt.ck_nohr.disable
	program time_tr_pr
	script max_from_enable
END

INCLUDE _constraints_sc 
INCLUDE sub_by_ifin_over_subpop
INCLUDE weights_fpi
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
	program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2
  DEFINE _y _top
  INCLUDE _sp_level
  DEFINE y @y

  CHECKBOX ck_nohr   _lft      _ls       _cwd1     .,			/*
		*/ label(resource LBL_DO_NO_REPORT_HAZARD_RATIOS)	/*
		*/ option("nohr")					/*
		*/
  CHECKBOX ck_tr     _lft2      @      @         .,			/*
		*/ label(resource LBL_REPORT_TIME_RATIOS)		/*
		*/ option("tratio")					/*
		*/
  CHECKBOX ck_noshow _lft      _ms       @         .,			/*
	*/ label(resource LBL_DO_NOT_REPORT_ST_VARIABLE_DESCRIPTIONS)	/*
		*/ option("noshow")					/*
		*/
  CHECKBOX ck_noskip		_lft	_ms	@	.,		/*
		*/label(resource LBL_PERFORM_LIKELIHOOD_RATIO_TEST)	/*
		*/option("noskip")

  DEFINE _x _lft
  DEFINE _y _ms
  DEFINE _cx _iwd
  INCLUDE _nocnsreport

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	option rpt.ck_nohr
	option rpt.ck_tr
	option rpt.ck_noshow
	option rpt.ck_noskip
	INCLUDE _nocnsreport_pr
	INCLUDE _report_columns_pr
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
END

INCLUDE intopts_fe
INCLUDE max_ml

PROGRAM command
BEGIN
	put /program by_output " "
	put /program se_prefix_output " "
	put "xtstreg "
	varlist [main.vl_iv]
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		option main.ck_nocons
		option radio(main rb_exp rb_weib rb_logn rb_logl rb_gamma)
		option main.ck_time
		INCLUDE _constraints_main_pr
		optionarg main.vn_offset
		option main.ck_collinear
		put " " /program se_output
		put " " /program rpt_output
		put " " /program intopts_fe_output
		put " " /program max_output
	endoptions
END
